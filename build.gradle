plugins {
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "java"
    id "scala"
    id "application"
}

group = "nl.hu.plagiaatdetectie"
version = "0.0.1-SNAPSHOT"
description = "plagiarism detection service"
java.sourceCompatibility = JavaVersion.VERSION_17

def scalaVersion = "2.12.1"
def scalaCompatVersion = "2.12"

repositories {
    mavenCentral()
}

dependencies {
	//Shared
	implementation(project("Library"))

    //Service
    implementation(group: "com.rabbitmq", name: "amqp-client", version: "5.17.0")
    implementation(group: "org.postgresql", name: "postgresql", version: "42.6.0")
    implementation(group: "com.google.code.gson", name: "gson", version: "2.10.1")
    implementation(group: "org.slf4j", name: "slf4j-api", version: "2.0.7")

    //Algorithm
	implementation(project("parsers"))
	implementation(group: "org.scala-lang", name: "scala-library", version: scalaVersion)
	implementation(group: "org.scala-lang.modules", name: "scala-parser-combinators_${scalaCompatVersion}", version: "2.3.0")
	implementation(group: "com.typesafe.play", name: "play-json_2.12", version: "2.9.4")

	implementation(group: "org.rogach", name: "scallop_${scalaCompatVersion}", version: "2.0.5")
	implementation(group: "ch.qos.logback", name: "logback-classic", version: "1.4.7")
	implementation(group: "com.typesafe.scala-logging", name: "scala-logging_${scalaCompatVersion}", version: "3.5.0")
	implementation(group: "org.antlr", name: "antlr4-runtime", version: "4.6")
	implementation(group: "eu.mihosoft.ext.apted", name: "apted", version: "0.1")
	implementation(group: "org.mozilla", name: "rhino", version: "1.7R3")

	implementation(group: "com.opencsv", name: "opencsv", version: "5.7.1")

    //Algorithm
	testImplementation(group: "org.scalatest", name: "scalatest_${scalaCompatVersion}", version: "3.2.9")
	testImplementation(group: "org.scalacheck", name: "scalacheck_${scalaCompatVersion}", version: "1.17.0")
}

tasks.compileScala.classpath = sourceSets.main.compileClasspath
tasks.compileJava.classpath += files(sourceSets.main.scala.classesDirectory)

tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = ["-unchecked", "-deprecation", "-feature", "-language:postfixOps", "-language:implicitConversions", "-opt:_"]
}

application {
    mainClass = "nl.hu.plagiaatdetectie.Main"
}

shadowJar {
	archiveBaseName.set("service")
	archiveClassifier.set("")
	archiveVersion.set("")
}